#########################################################################################################
#
# BiSSE Passerine Bird Carotenoids
# Presence-Absence of carotenoid coloration
# Veronica Rincon Rubio
# Last Updated: 10/25/2019
#########################################################################################################


#######################
# Reading the Data #
#######################
setOption("useScaling","true")
NUM_STATES = 2

### Read in the data

data = readCharacterDataDelimited(file="passerdiet/passerdiet/Data/pasercar.tsv", stateLabels=2, type="NaturalNumbers", delimiter="\t", headers=FALSE)

### Reading phylogeny
phylogeny <- readTrees(file="passerdiet/passerdiet/Data/passertree.nex")[1]


# Get some useful variables from the data. We need these later on.

taxa <- phylogeny.taxa()

# set my move index
mvi = 0 ## This is a counter for moves, the things that we propose to change the values of the parameters in the MCMC
mni = 0 ##This is a counter for monitors, the things that we use to follow the MCMC and store the results.

H = 0.587405



####################
# Create the rates #
####################


### Create the constant prior parameters of the diversification rates
## Number of surviving lineages is 5983
rate_mean <- ln( ln(5983/2.0) / phylogeny.rootAge() )
rate_sd <- 2*H


for (i in 1:NUM_STATES) {

### Create a lognormal distributed variable for the diversification rate
log_speciation[i] ~ dnNormal(mean=rate_mean,sd=rate_sd)
log_ speciation[i].setValue( rate_mean )
speciation[i] := exp( log_speciation[i] )
moves[++mvi] = mvSlide(log_speciation[i],delta=0.20,tune=true,weight=3.0)

### Create a lognormal distributed variable for the turnover rate
log_extinction[i] ~ dnNormal(mean=rate_mean,sd=rate_sd)
log_extinction[i].setValue( rate_mean )
extinction[i] := exp( log_extinction[i] )
moves[++mvi] = mvSlide(log_extinction[i],delta=0.20,tune=true,weight=3)

}


#########################################################
# Set up the transition rate matrix for observed states #
#########################################################
## I defined very loosely my gamma priors for rates in the Q-mat
shape_pr <- 0.5
#rate_pr := 1

############### Alternative definition or rate parameter
# Each transition rate between observed states are drawn
# from an exponential distribution with a mean of 37
# character state transitions over the tree.
rate_pr := phylogeny.treeLength() / 40 ###Esto lo tengo que cambiar de acuerdo a lo que diga el articulo de Thomas

rate_12 ~ dnGamma(shape=shape_pr, rate=rate_pr)
rate_21 ~ dnGamma(shape=shape_pr, rate=rate_pr)

moves[++mvi] = mvScale( rate_12, weight=2 )
moves[++mvi] = mvScale( rate_21, weight=2 )


######################################################################
# Create the rate matrix #
######################################################################
rate_matrix := fnFreeBinary( [rate_12, rate_21 ], rescaled=false)


#####################################
# Set up the root state frequencies #
# We're going to estimate them and not set them up#
#####################################


### Create a constant variable with the prior probabilities of each rate category at the root.
rate_category_prior ~ dnDirichlet( rep(1,NUM_STATES) )
moves[++mvi] = mvDirichletSimplex(rate_category_prior,tune=true,weight=2)

### Create the fix parameter for the age of the root set to the observed age
root <- phylogeny.rootAge()


### I did not add any sampling of species but it is posible by setting this rho parameter
### rho is the probability of sampling species at the present
### fix this to 5022/5983
rho <- phylogeny.ntips()/5983


####################################################################
# Building the BiSSE Model as discrete character model+ BD process#
###################################################################

### Here is where I tie speciation, extinction, and Q using a Birth-Death with categories
timetree ~ dnCDBDP( rootAge           = root,
speciationRates   = speciation,
extinctionRates   = extinction,
Q                 = rate_matrix,
pi                = rate_category_prior,
rho               = rho,
delta             = 1.0,
condition         = "time" )


### clamp the model with the "observed" tree
timetree.clamp( phylogeny )
timetree.clampCharData( data )


#############
# The Model #
#############


### workspace model wrapper ###
mymodel = model(rate_matrix)


### set up the monitors that will output parameter values to file and screen
monitors[++mni] = mnModel(filename="passerdiet/passerdiet/Data/output/BiSSE_passercar.log", printgen=1)
monitors[++mni] = mnJointConditionalAncestralState(tree=timetree, cdbdp=timetree, type="NaturalNumbers", printgen=100, withTips=true, withStartStates=false, filename="passerdiet/passerdiet/Data/output/anc_states_BiSSE_passercar.log")
monitors[++mni] = mnScreen(printgen=10, rate_12, rate_21, speciation, extinction)


################
# The Analysis #
################

### workspace mcmc
mymcmc = mcmc(mymodel, monitors, moves, nruns=1, moveschedule="random")

### pre-burnin to tune the proposals 20% of the sample
mymcmc.burnin(generations=5000,tuningInterval=100)


### run the MCMC
mymcmc.run(generations=250000)


##############################
# Summarize ancestral states #
##############################

#anc_states = readAncestralStateTrace("passerdiet/passerdiet/Data/output/anc_states_BiSSE_passerdiet.log")
#anc_tree = ancestralStateTree(tree=phylogeny, ancestral_state_trace_vector=anc_states, include_start_states=false, file=""passerdiet/passerdiet/Data/output/output/anc_states_summaryBiSSEtreeCAR.tree", burnin=5000, summary_statistic="MAP", site=0)

q()






